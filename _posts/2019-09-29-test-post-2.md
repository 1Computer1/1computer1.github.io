---
title: "Test Post 2"
date: 2019-09-29
tags: General
---

This article has code blocks in it! It's gonna be a little longer too!  

# Code

Some simple stuff.  
It prints `1`, `2`, and `3` on separate lines.  

```hs
main :: IO ()
main = do
    print 1
    print 2
    print 3
```

# More Code

Fancy stuff!  

```hs
{-# LANGUAGE AllowAmbiguousTypes   #-}
{-# LANGUAGE DataKinds             #-}
{-# LANGUAGE FlexibleContexts      #-}
{-# LANGUAGE FlexibleInstances     #-}
{-# LANGUAGE KindSignatures        #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE ScopedTypeVariables   #-}
{-# LANGUAGE TypeApplications      #-}
{-# LANGUAGE TypeFamilies          #-}
{-# LANGUAGE TypeOperators         #-}

import Data.Kind

data Nat = Z | S Nat

newtype Nested (n :: Nat) (f :: Type -> Type) (a :: Type)
    = Nested { unnest :: Inner n f a }

type family Inner (n :: Nat) (f :: Type -> Type) (a :: Type) :: Type where
    Inner 'Z f a = a
    Inner ('S n) f a = f (Nested n f a)

instance Functor (Nested 'Z f) where
    fmap f (Nested xs) = Nested (f xs)

instance (Functor f, Functor (Nested n f)) => Functor (Nested ('S n) f) where
    fmap f (Nested xs) = Nested (fmap (fmap f) xs)

type family Expand (n :: Nat) (f :: Type -> Type) (a :: Type) :: Type where
    Expand 'Z f a = a
    Expand ('S n) f a = f (Expand n f a)

class Expandable (n :: Nat) (f :: Type -> Type) where
    expand :: Nested n f a -> Expand n f a
    shrink :: Expand n f a -> Nested n f a

instance Expandable 'Z f where
    expand = unnest
    shrink = Nested

instance (Functor f, Expandable n f) => Expandable ('S n) f where
    -- This duality is awesome!
    expand = fmap expand . unnest
    shrink = Nested . fmap shrink

-- AllowAmbiguousTypes was needed here
-- Otherwise we had to pass in `Proxy n` and `Proxy f`
fmapNested :: forall n f a b. (Functor f, Functor (Nested n f), Expandable n f)
           => (a -> b)
           -> Expand n f a
           -> Expand n f b
fmapNested f = expand . fmap f . shrink @n @f

type Zero  = 'Z
type One   = 'S Zero
type Two   = 'S One
type Three = 'S Two

main :: IO ()
main = do
    let xs = [["ifmmp", "xpsme"], ["hppeczf", "xpsme"]]
    print $ fmapNested @Three pred xs
```
